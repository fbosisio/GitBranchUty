#!/usr/bin/env perl
#------------------------------------------------------------------------------
# $Id$
#------------------------------------------------------------------------------

use warnings;
use strict;

use constant TRUE  => (1 == 1);
use constant FALSE => (! TRUE);

use Getopt::Long;
#use Data::Dumper;

use lib ( __FILE__ =~ m!^(.*)/[^\/]+$! );  # Next module can be in same dir
use GitCmd;

# Only default values different from FALSE must be listed here ...
# Default for 'mode' is not listed, since it can also be read from git config
my %OPTIONS = (
  fetch   => TRUE,
  restore => TRUE,
  pretty  => TRUE
);

my %COLORS = (
  I => '[33;40m ',
  B => '[30;42m ',
  N => ' [m'
);

my %STATUS = ();

# Here $GIT_R (R for "real") ALWAYS invokes the various git commands,
# whereas $GIT_D (D for "dryrun") obeys the "--dryrun" option (run or print).
my $GIT_R = undef;
my $GIT_D = undef;

{ # -----[ main ]-----

  &ManageOptions();

  $GIT_R = new GitSynch( verbose => ( $OPTIONS{verbose} ? 1 : 0 ) );
  $GIT_D = $GIT_R->new( printOnly => $OPTIONS{dryrun} );

  my $remote = undef;
  my @branch_list = ();
  if ( $#ARGV >= 0 ) {
    $STATUS{git_dir} = $GIT_R->GitDir;
    foreach my $arg (@ARGV) {
      if ( -d "$STATUS{git_dir}/refs/remotes/$arg" ) {
        die "You cannot specify more then one <remote> !\n" if ( defined($remote) );
        $remote = $arg;
      } else {
        push( @branch_list, $arg );
      }
    } # END foreach
  }

  if ( $OPTIONS{all} && ($#branch_list >= 0) ) {
    die "You cannot specify both '--all' and a branch name !\n";
  }

  if ( $OPTIONS{fetch} ) {
    &RunGitFetch( $remote || '', TRUE );
  }

  if ( $OPTIONS{all} ) { # Process all remote tracking branches
    @branch_list = $GIT_R->TrackingBranches( $remote );
    foreach my $elem (@branch_list) {
      my( $br, $rem ) = @{$elem};
      &Info( "Checking branch $br" );
      &SynchBranch( $br, $rem );
    } # END foreach
  } elsif ( $#branch_list > 0 ) { # Several branches listed: process all them
    foreach my $br (@branch_list) {
      &Info( "Checking branch $br" );
      &SynchBranch( $br, $remote );
    } # END foreach
  } else { # Process just a single branch (the only one listed or the current)
    my $branch = $branch_list[0];
    if ( ! defined($branch) ) {
      $branch = $STATUS{orig_branch} = $GIT_R->CurrentBranch;
    }
    &SynchBranch( $branch, $remote );
  }

  # If $STATUS{active_branch} is defined, also $STATUS{orig_branch} is defined.
  if ( exists($STATUS{active_branch}) &&
       ( $STATUS{active_branch} ne $STATUS{orig_branch} ) ) {
    if ( $OPTIONS{restore} || exists($STATUS{stash_saved}) ) {
      &Info( "Running 'git checkout $STATUS{orig_branch}'" );
      $GIT_D->checkout( $STATUS{orig_branch} );
    } else {
      print "Leaving repository on branch '$STATUS{active_branch}'\n",
            "(run 'git checkout $STATUS{orig_branch}' to revert-back)\n";
    }
  }

  if ( exists($STATUS{stash_saved}) ) {
    &SubInfo( "Restoring work-in-progress saved with 'git stash'" );
    $GIT_D->stash( 'pop' );
  }

} # -----[ main ]-----

#==============================================================================

sub SynchBranch {
  # ----- Function arguments -----
  my( $branch, $remote ) = @_;
  # ----- Function code -----
  $remote = $GIT_R->UpstreamBranch( $branch ) unless ( defined($remote) );

  $STATUS{git_dir} = $GIT_R->GitDir unless ( exists($STATUS{git_dir}) );
  my $remote_ref = "refs/remotes/$remote";
  $remote_ref = "refs/remotes/$remote/$branch" if ( -d "$STATUS{git_dir}/$remote_ref" );

  my( $remote_name ) = ( $remote_ref =~ m!/([^/]+)/[^/]+$! );

  my $max = 6;
  my $count = 1;
  for ( ; $count <= $max; ++$count ) {
    my $local_br_hash = $GIT_R->RefToHash( "refs/heads/$branch" );
    my $remote_br_hash = $GIT_R->RefToHash( $remote_ref );
    my $common_ancestor = $GIT_R->merge_base( $local_br_hash, $remote_br_hash );
    print "\t\tmerge_base = $common_ancestor\n" if ( $OPTIONS{verbose} );

    if ( $common_ancestor eq $remote_br_hash ) {
      if ( $common_ancestor eq $local_br_hash ) {
        #- - - - - - - - - - - - - - - - - - - - - - - - - - - - - -#
        # 1) LOCAL = COMMON = REMOTE                 common         #
        #    (already "up-to-date")              local o remote     #
        #                                              |            #
        #- - - - - - - - - - - - - - - - - - - - - - - - - - - - - -#
        print "Already up-to-date.\n";
        last;
      } else {
        #- - - - - - - - - - - - - - - - - - - - - - - - - - - - - -#
        # 2) LOCAL != COMMON = REMOTE          local o              #
        #    (only a "push" is needed)                \             #
        #                                       common o remote     #
        #                                              |            #
        #- - - - - - - - - - - - - - - - - - - - - - - - - - - - - -#
        last if &ModeOptionBlocksOper( 'push', 'push', $branch, 'pull' );
        &SubInfo( "Pushing branch '$branch'" );
        my $safe_dry_git  = $GIT_D->new( stopOnErrors => FALSE );
        $safe_dry_git->push( $remote_name, $branch );
        last unless ( $safe_dry_git->KO );
        # Maybe someone else just pushed something in the meanwhile.
        # So perform a "fetch" and then retry the whole alignment ...
        &SubInfo( "Push failed: trying pull first" );
        &RunGitFetch( $remote_name, FALSE );
        # Let the loop go on (it will be step 3 or 4)
      }
    } else { # --- ( $common_ancestor ne $remote_br_hash ) ---
      if ( $common_ancestor eq $local_br_hash ) {
        #- - - - - - - - - - - - - - - - - - - - - - - - - - - - - -#
        # 3) LOCAL = COMMON != REMOTE                    o remote   #
        #   (only a "pull" is needed)                   /           #
        #                                        local o common     #
        #                                              |            #
        #- - - - - - - - - - - - - - - - - - - - - - - - - - - - - -#
        last if &ModeOptionBlocksOper( 'pull', 'pull', $branch, 'push' );
        &SubInfo( "Pulling branch '$branch' (via FF merge)" );
        $STATUS{orig_branch} = $GIT_R->CurrentBranch unless ( exists($STATUS{orig_branch}) );
        my $head_br = ( exists($STATUS{active_branch}) ?
                        $STATUS{active_branch} : $STATUS{orig_branch} );
        $GIT_D->FastForwardMerge( $branch, $remote, $local_br_hash,
                                  $remote_br_hash, $head_br );
        last;
      } else {
        #- - - - - - - - - - - - - - - - - - - - - - - - - - - - - -#
        # 4) LOCAL != COMMON != REMOTE         local o   o remote   #
        #   (both "pull" and "push" needed)           \ /           #
        #                                              o common     #
        #                                              |            #
        #- - - - - - - - - - - - - - - - - - - - - - - - - - - - - -#
        last if &ModeOptionBlocksOper( 'both pull and push', 'pull', $branch );
        if ( &IsCheckoutNeeded($branch) ) {
          $STATUS{active_branch} = $branch;
          &SubInfo( "Running 'git checkout $branch'" );
          $GIT_D->checkout( $branch );
        }
        # Here we only do the 'pull' step, then we re-enter the loop
        # in order to perform the 'push' (it will be step 2 above)
        &ExecuteGitPull( $branch, $remote, $local_br_hash, $remote_name,
                         $common_ancestor );
        # Since we still need to do the "push", we don't stop the loop ...
        if ( $OPTIONS{dryrun} ) { # ... unless in 'dryrun' mode
          print "Please note we cannot test the 'push' phase in dryrun mode.\n";
          last;
        }
      }
    }
  } # END for
  die "Maximum number of iterations ($max) reached!\n" if ( $count >= $max );
}

#==============================================================================

sub SetDefaultFromGitConfig {
  # ----- Function arguments -----
  my( $safe_git, $git_cmd_name, $param_name, $valid_values, $opt_name ) = @_;
  # ----- Local variables -----
  my $full_parameter = "$git_cmd_name.$param_name";
  my $msg_format = "\tOption '%s' implied by git config.\n";
  # ----- Function code -----
  # Search git config-files for the given param (returns '' if not found)
  my $value = $safe_git->config( '--get', $full_parameter );
  return if ( $value eq '' ); # Do nothing if parameter not in config files

  if ( $valid_values eq 'BOOL_FLAG' ) {
    if ( $value ) { # If $value is FALSE, just leave unset
      $OPTIONS{$opt_name} = TRUE;
      printf $msg_format, "--$opt_name" if ( $OPTIONS{verbose} );
    }
  } elsif ( $value =~ /^(?:$valid_values)$/ ) {
    $OPTIONS{$opt_name} = $value;
    printf $msg_format, "--$opt_name=$value" if ( $OPTIONS{verbose} );
  } else {
    $valid_values =~ s/\|([^|]+)$/' or '$1/;
    $valid_values =~ s/\|/', '/g;
    die "Invalid value '$value' for '$full_parameter' in git config:\n" .
        "must be one of '$valid_values' !\n ";
  }
}

#==============================================================================

sub ManageOptions {
  # ----- Local variables -----
  my $valid_modes = 'check|pull|push|auto|ask|mixed';
  my %config_params = (
    mode   => 'defaultmode',
    rebase => 'rebasepull',
    stash  => 'stashchanges'
  );
  # ----- Function code -----
  &GetOptions( \%OPTIONS,
               'verbose|v',
               'dryrun|dry-run|n',
               'pretty!',
               'fetch!',
               'restore!',
               'rebase',
               'stash',
               'author=s',
               'mode=s',
               'all',
               'ignore-config',
               'do-not-check-status', # Undocumented ....
               'help|h|?' ) or die "ERROR: option parsing failed!\n";
  #print &Dumper( \%OPTIONS );

  if ( $OPTIONS{help} ) {
    my( $program_name ) = ( $0 =~ m!([^/]+)$! );
    $program_name =~ s/^(git)-(.*)$/$1 $2/;
    my $cmd_name = $2;
    my $indent_space = ' ' x length($program_name);
    print STDERR "
  USAGE: $program_name [-v|--verbose] [-n|--dry[-]run] [--nopretty] [--nofetch]
         $indent_space [--norestore] [--rebase] [--stash] [--author=<author>]
         $indent_space [--mode={$valid_modes}] [<remote>]
         $indent_space [--all | <branch> [<branch> ...]]\n
         --verbose    : print each underlying git command before execution
         --dryrun     : just print git commands, but do not execute them
         --nopretty   : do not use ANSI colors to highlight step messages
         --nofetch    : skip 'git fetch' before starting 'push/pull'
         --norestore  : do not revert back to original branch (if changed)
         --rebase     : use rebase instead of merge when pulling
         --stash      : stash changes if checkout needed (default is to ask)
         --author=<x> : use 'x' as author of the merge commit (if not ff)
         --mode=check : just tell if push or pull are needed, but do nothing
         --mode=pull  : pull without asking (but do not push)
         --mode=push  : push without asking (also pull in case of conflicts)
         --mode=auto  : do both pull and push as required, whithout asking
         --mode=ask   : (default) do both pull and push, but with confirmation
         --mode=mixed : pull without asking but ask when push is required\n
  If '<remote>' is omitted, it defaults to the tracked remote of '<branch>'.
  If '<branch>' is omitted, it defaults to the current branch.
  If '--all' is used, then all tracking branches (of <remote>) are synchronized.
  To change default 'mode' use 'git config [--global] $cmd_name.$config_params{mode} <mode>'.
\n";
    exit 0;
  }

  unless ( exists($OPTIONS{'ignore-config'}) ) {
    my $mode_found = exists($OPTIONS{mode});
    my $rebase_found = exists($OPTIONS{rebase});
    my $stash_found = exists($OPTIONS{stash});
    unless ( $mode_found && $rebase_found && $stash_found ) {
      my( $cmd_name ) = ( $0 =~ m!(?:git-)?([^/]+)$! );
      my $safe_git = new GitCmd( stopOnErrors => FALSE,
                                 verbose => ( $OPTIONS{verbose} ? 1 : 0 ) );
      &SetDefaultFromGitConfig( $safe_git, $cmd_name, $config_params{mode},
                                $valid_modes, 'mode' ) unless ( $mode_found );
      &SetDefaultFromGitConfig( $safe_git, $cmd_name, $config_params{rebase},
                                'BOOL_FLAG', 'rebase' ) unless ( $rebase_found );
      &SetDefaultFromGitConfig( $safe_git, $cmd_name, $config_params{stash},
                                'BOOL_FLAG', 'stash' ) unless ( $stash_found );
    }
  }
  # Fallback to hard-coded default
  $OPTIONS{mode} = 'ask' unless ( exists($OPTIONS{mode}) );

  if ( $OPTIONS{mode} !~ /^(?:$valid_modes)$/o ) {
    $valid_modes =~ s/\|([^|]+)$/' or '$1/;
    $valid_modes =~ s/\|/', '/g;
    die "Value '$OPTIONS{mode}' not valid for option '--mode':\n" .
        "must be one of '$valid_modes' !\n ";
  }

  $COLORS{I} = $COLORS{B} = $COLORS{N} = '' unless ( $OPTIONS{pretty} );
}

#==============================================================================

sub Info {
  print "\t$COLORS{I}@_ ...$COLORS{N}\n";
}

#==============================================================================

sub SubInfo {
  print "\t$COLORS{B}@_ ...$COLORS{N}\n";
}

#==============================================================================

sub AskForConfirmation {
  # ----- Function arguments -----
  my( $msg ) = @_;
  # ----- Function code -----
  while ( TRUE ) {
    printf '%s [Y/N] » ', $msg;
    if ( $OPTIONS{dryrun} ) { print "Y [dryrun]\n"; return TRUE; }
    chomp( my $answer = <STDIN> );
    return TRUE  if ( $answer =~ /^y$/i );
    return FALSE if ( $answer =~ /^n$/i );
  } # END while
}

#==============================================================================

sub ModeOptionBlocksOper {
  # ----- Function arguments -----
  my( $required_opers, $oper, $branch, $forbidden_mode ) = @_;
  # ----- Local variables -----
  my $stop = FALSE;
  my $selected_mode = $OPTIONS{mode};
  # ----- Function code -----
  if ( $selected_mode eq 'mixed' ) {
    # Option 'mixed' means that 'pull' is automatic while 'push' shall ask
    $selected_mode = ( $oper eq 'pull' ) ? 'pull' : 'ask';
  }

  if ( $selected_mode eq 'check' ) {
    print "\t==> $required_opers required.\n";
    $stop = TRUE;
  } elsif ( $selected_mode eq 'ask' ) {
    my $ans = &AskForConfirmation( "[branch $branch] Proceed with $oper ?" );
    $stop = ( ! $ans ); # Stop if answer is 'N'
  } elsif ( defined($forbidden_mode) && ($selected_mode eq $forbidden_mode) ) {
    print "\t==> $oper skipped (since mode=$selected_mode).\n";
    $stop = TRUE;
  }
  # ----- Return value -----
  $stop;
}

#==============================================================================

sub IsCheckoutNeeded {
  # ----- Function arguments -----
  my( $branch ) = @_;
  # ----- Local variables -----
  my $checkout_needed;
  # ----- Function code -----
  if ( exists($STATUS{active_branch}) ) {
    $checkout_needed = ( $STATUS{active_branch} ne $branch );
  } else {
    $STATUS{orig_branch} = $GIT_R->CurrentBranch unless ( exists($STATUS{orig_branch}) );
    $checkout_needed = ( $STATUS{orig_branch} ne $branch );
    if ( $checkout_needed ) {
      # First checkout ==> check 'git status' before continuing
      &SubInfo( "Running 'git status'" );
      if ( $OPTIONS{'do-not-check-status'} ) {
        print "Warning: check of 'git status' skipped as requested !!!\n";
      } elsif ( ! $GIT_R->StatusIsClean ) {
        unless ( exists($OPTIONS{stash}) ) {
          my $msg = 'git status not clean: save current work and proceed ?';
          my $ans = &AskForConfirmation( $msg );
          exit 1 if ( ! $ans ); # Stop if answer is 'N'
        }
        &SubInfo( "Saving work-in-progress with 'git stash'" );
        $GIT_D->stash( 'save' );
        $STATUS{stash_saved} = TRUE;
        # Re-check for enhanced safety ...
        &SubInfo( "Checking 'git status' again" );
        die "git status is not clean after git stash!\n" if ( ! $GIT_R->StatusIsClean );
      }
    }
  }
  # ----- Return value -----
  $checkout_needed;
}

#==============================================================================

sub ExecuteGitPull {
  # ----- Function arguments -----
  my( $branch_name, $remote, $br_hash, $remote_name, $ancestor_hash ) = @_;
  # ----- Function code -----
#  my $use_rebase = $OPTIONS{rebase};
#  if ( $use_rebase && &CommitRangeIncludesAMerge($ancestor_hash,$br_hash) ) {
#    # It seems better not to rebase a merge-commit ...
#    # ... or can we use 'git rebase -p' to preserve merges ???
#    print "Option '--rebase' ignored since '$branch_name' includes a merge.\n";
#    $use_rebase = FALSE;
#  }

  my @opts = ( '--pretty' );
  push( @opts, '--verbose' ) if ( $OPTIONS{verbose} );
  push( @opts, '--dryrun' ) if ( $OPTIONS{dryrun} );

#  if ( $use_rebase ) {
#    &SubInfo( "Running 'git pull --rebase'" );
#    $GIT_D->pull( '--rebase' );
  if ( $OPTIONS{rebase} ) {
    &SubInfo( "Running 'git do-rebase'" );
    # Here we use "$GIT_R" since we also pass "--dryrun" option (if present)
    $GIT_R->do_rebase( @opts, '-p', $remote ); # '-p' means "preserve merges"
  } else {
    push( @opts, "--author='$OPTIONS{author}'" ) if ( $OPTIONS{author} );
    unless ( exists($STATUS{fetch_host}) ) {
      &SubInfo( "Running 'git remote' to get repo URL" );
      $STATUS{fetch_host} = $GIT_R->FetchUrl( $remote_name );
    }
    my $msg ="Merge $remote ($STATUS{fetch_host}) into $branch_name";
    &SubInfo( "Running 'git do-merge'" );
    # Here we use "$GIT_R" since we also pass "--dryrun" option (if present)
    $GIT_R->do_merge( @opts, '-m', "'$msg'", $remote );
  }
}

#==============================================================================

sub RunGitFetch {
  # ----- Function arguments -----
  my( $remote, $print ) = @_;
  # ----- Function code -----
  if ( ( $OPTIONS{mode} ne 'check' ) && ( $^O eq 'msys' ) ) { # Only on WIN
    # Apparently, it seems that sometimes this command has problems when
    # run automatically instead of explicitly ... (but only on this OS)
    &Info( 'Compacting repository (if needed)' ) if ( $print );
    $GIT_R->gc( '--auto' );
  }
  &Info( 'Fetching remote repository' ) if ( $print );
  $GIT_R->fetch( $remote );
}

#==============================================================================
#==============================================================================

package GitSynch;

use IO::Handle;

use lib ( __FILE__ =~ m!^(.*)/[^\/]+$! );  # Next module can be in same dir
use base 'GitCmd';

use constant TRUE  => GitCmd::TRUE;
use constant FALSE => GitCmd::FALSE;

#------------------------------------------------------------------------------

sub StatusIsClean {
  # ----- Function arguments -----
  my( $self ) = @_;
  # ----- Local variables -----
  my @status = ();
  # ----- Function code -----
  my $fh = new IO::Handle;
  $self->status( $fh, '--untracked-files=no' );
    while ( my $line = <$fh> ) {
      if ( $line =~ /nothing\s+to\s+commit/ ) { @status = (); last; }
      push( @status, $line );
    }
  $self->Close( $fh );
  # Print command-output (if status is clean, list is empty so print nothing)
  foreach my $line (@status) { print $line; }
  # ----- Return value -----
  ( $#status < 0 );
}

#------------------------------------------------------------------------------

sub CurrentBranch {
  # ----- Function arguments -----
  my( $self ) = @_;
  # ----- Function code -----
  my $current = $self->symbolic_ref( 'HEAD' );
  $current =~ s!^refs/heads/!!;
  # ----- Return value -----
  $current;
}

#------------------------------------------------------------------------------

# The logic of this routine is based on the shell script found at:
# http://stackoverflow.com/questions/4950725/how-do-i-get-git-to-show-me-which-branches-are-tracking-what
sub TrackingBranches {
  # ----- Function arguments -----
  my( $self, $remote ) = @_;
  # ----- Local variables -----
  my @list = ();
  # ----- Function code -----
  my $fh = new IO::Handle;
  my $safe_git = $self->new( stopOnErrors => FALSE );
  $self->for_each_ref( $fh, "--format='%(refname:short)'", 'refs/heads/*' );
    while ( my $br = <$fh> ) {
      chomp( $br );
      my $upstr = $safe_git->UpstreamBranch( $br );
      next if ( $safe_git->KO );
      next if ( defined($remote) && ( $upstr !~ m!^$remote/! ) );
      push( @list, [ $br, $upstr ] );
    }
  $self->Close( $fh );
  # ----- Return value -----
  @list;
}

#------------------------------------------------------------------------------

# The logic of this routine is based on (part of) the shell script found at:
# http://stackoverflow.com/questions/4156957/merging-branches-without-checkout/4157435#4157435
sub FastForwardMerge {
  # ----- Function arguments -----
  my( $self, $branch, $commit, $branch_hash, $commit_hash, $head_br ) = @_;
  # ----- Function code -----
  if ( $branch eq $head_br ) {
    # If given "$branch" is the HEAD, perform a real "git merge" in order
    # to update also the working tree (and not only the repository)
    $self->merge( '--ff-only', $commit );
  } else {
    my( $branch_short_hash ) = ( $branch_hash =~ /^(\w{7})/ );
    my( $commit_short_hash ) = ( $commit_hash =~ /^(\w{7})/ );
    print "Updating $branch_short_hash..$commit_short_hash\n";
    $self->update_ref( '-m', "'Merge $commit: fast forward'",
                       "'refs/heads/$branch'", $commit_hash, $branch_hash );

    print "Fast forward\n";
    my $safe_git = $self->new( stopOnErrors => FALSE );
    $safe_git->diff( '--stat', "'$branch\@{1}'", "'$branch'" );
    warn "WARNING: execution of 'git diff' failed.\n" if ( $safe_git->KO );
  }
}

#------------------------------------------------------------------------------

sub FetchUrl {
  # ----- Function arguments -----
  my( $self, $remote ) = @_;
  # ----- Local variables -----
  my $fetch_addr;
  # ----- Function code -----
  my $fh = new IO::Handle;
  $self->remote( $fh, '-v' );
    while ( my $line = <$fh> ) {
      if ( $line =~ /^$remote\s.*\@([^.]+)\..*\(fetch\)/ ) {
        $fetch_addr = $1;
        last;
      }
    }
  $self->Close( $fh );
  # ----- Return value -----
  $fetch_addr;
}

#------------------------------------------------------------------------------

sub GitDir {
  # ----- Function arguments -----
  my( $self ) = @_;
  # ----- Return value -----
  $self->rev_parse( '--git-dir' );
}

#------------------------------------------------------------------------------

sub RefToHash {
  # ----- Function arguments -----
  my( $self, $ref ) = @_;
  # ----- Return value -----
  $self->show_ref( '-s', '--verify', "'$ref'", '2>/dev/null' );
}

#------------------------------------------------------------------------------

sub UpstreamBranch {
  # ----- Function arguments -----
  my( $self, $branch ) = @_;
  # ----- Return value -----
  $self->rev_parse( '--abbrev-ref', "$branch\@{upstream}", '2>/dev/null' );
}

#------------------------------------------------------------------------------

# module return code
TRUE;

#==============================================================================
#==============================================================================
