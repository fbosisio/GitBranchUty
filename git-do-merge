#!/usr/bin/env perl
#------------------------------------------------------------------------------
# $Id$
#------------------------------------------------------------------------------

use warnings;
use strict;

use constant TRUE  => (1 == 1);
use constant FALSE => (! TRUE);

use Getopt::Long;
#use Data::Dumper;

# Only default values different from FALSE must be listed here ...
my %OPTIONS = ( interact => TRUE );

my %COLORS = (
  I => '[33;40m',
  B => '[30;42m',
  N => '[m',
  S => ' '
);

{ # -----[ main ]-----

  my $br_to_merge = undef;
  my @merge_opts = ();
  &ManageOptions( \$br_to_merge, \@merge_opts );

  # Here $git_r (R for "real") ALWAYS invokes the various git commands,
  # whereas $git_d (D for "dryrun") obeys the "--dryrun" option (run or print).
  # Finally $git_rs and $git_ds (S for "safe") are like $git_r and $git_d 
  # respectively, but do not fail on errors.
  my $git_r = new GitDoMerge( verbose => ( $OPTIONS{verbose} ? 1 : 0 ) );
  my $git_d = $git_r->new( printOnly => $OPTIONS{dryrun} );
  my $git_rs = $git_r->new( stopOnErrors => FALSE );
  my $git_ds = $git_d->new( stopOnErrors => FALSE );

  my $curr_br = $git_r->CurrentBranch;

  my $ask_before_commit = $OPTIONS{confirm};
  my $abort_merge = FALSE;

  &Info( "Merging branch '$br_to_merge' into '$curr_br'" );
  $git_ds->merge( $br_to_merge, '--no-commit', @merge_opts );

  if ( $git_ds->KO ) {
    $ask_before_commit = TRUE;
    if ( $OPTIONS{'fail-if-conflict'} ) {
      $abort_merge = TRUE;
      $OPTIONS{interact} = FALSE; # Disable interaction, to force aborting
    } else {
      my @opts = ();
      push( @opts, '--verbose' ) if ( $OPTIONS{verbose} );
      push( @opts, '--dryrun' ) if ( $OPTIONS{dryrun} );
      push( @opts, '--pretty' ) if ( $OPTIONS{pretty} );
      push( @opts, '--nointeract' ) unless ( $OPTIONS{interact} );
      push( @opts, '--edit' ) if ( $OPTIONS{edit} );
      # Here we use "$git_rs" since we also pass "--dryrun" option (if present)
      $git_rs->resolve_conflicts( @opts );
      $abort_merge = $git_rs->KO;
    }
  }

  # If there was nothing to merge, we are done
  unless ( $git_r->MergeInProgress || $OPTIONS{dryrun} ) {
    print "No file needs merging: job done.\n";
    exit 0;
  }

  if ( ( ! $abort_merge ) && exists($OPTIONS{run}) ) {
    my $command = $OPTIONS{run};
    &Info( "Running '$command' to validate the merge" );
    $command = "echo '$command'" if ( $OPTIONS{dryrun} );
    system( $command ) == 0 or do {
      print "Execution of '$command' failed: aborting.\n";
      $ask_before_commit = $abort_merge = TRUE;
    }
  }

  if ( $OPTIONS{'auto-undo'} ) {
    &Info( 'Automatically undoing merge as requested' );
    $git_d->merge( '--abort' );
    exit 0;
  }

  if ( $ask_before_commit ) {
    my $ans = FALSE;
    unless ( $abort_merge ) {
      print "\nYou can now $COLORS{B}commit$COLORS{N} the merge, leave it",
            " $COLORS{B}pending$COLORS{N} or $COLORS{B}abort$COLORS{N} it.\n";
      $ans = &AskConfirmation( 'Proceed with merge ?', TRUE );
      print "OK, aborting.\n" if ( ! $ans ); # i.e. answer is 'N'
    }
    if ( ! $ans ) { # i.e. answer is 'N'
      &AbortMerge( $git_d );
      exit 1;
    }
  }

  &Info( "Running 'git commit' to conclude merge" );
  my @commit_opts = ();
  if ( exists($OPTIONS{message}) ) {
    push( @commit_opts, ('-m', "'$OPTIONS{message}'") );
  } elsif ( ! $OPTIONS{interact} ) {
    push( @commit_opts, ('-m', "'Merged $br_to_merge into $curr_br'") );
  }
  if ( exists($OPTIONS{author}) ) {
    push( @commit_opts, "--author='$OPTIONS{author}'" );
  }
  $git_d->commit( @commit_opts );

} # -----[ main ]-----

#==============================================================================

sub AbortMerge {
  # ----- Function arguments -----
  my( $git_d ) = @_;
  # ----- Function code -----
  print "What do you want to do with current merge?\n" .
        " - cancel it (you need to REDO the merge manually then !)\n" .
        " - leave it pending (you can make changes and commit the merge).\n";
  my $ans = &AskConfirmation( 'Undo merge and revert to original state ?', TRUE );
  if ( $ans ) { # Answer is 'Y'
    &Info( "Undoing merge with 'git merge --abort'" );
    $git_d->merge( '--abort' );
    &Info( "Re-showing 'git status'" );
    $git_d->status( '--untracked-files=no' );
  } else {  # Answer is 'N'
    print "\nWARNING: leaving the merge pending as requested !\n" .
          "\t (complete with \"git commit\" or undo with \"git merge --abort\")\n\n";
  }
}

#==============================================================================

sub ManageOptions {
  # ----- Local variables -----
  my( $branch_to_merge, $options_for_merge ) = @_;
  # ----- Function code -----
  # Leave unrecognized options in ARGV, instead of flagging them as errors
  &Getopt::Long::Configure( 'pass_through' );

  &GetOptions( \%OPTIONS,
               'verbose|v',
               'dryrun|dry-run|n',
               'pretty',
               'interact!',
               'edit|e',
               'run=s',
               'confirm',
               'auto-undo',
               'fail-if-conflict',
               'message|m=s',
               'author=s',
               'help|h|?' );
  #print &Dumper( \%OPTIONS );

  if ( $OPTIONS{help} ) {
    my( $program_name ) = ( $0 =~ m!([^/]+)$! );
    $program_name =~ s/^(git)-/$1 /;
    my $indent_space = ' ' x length($program_name);
    print STDERR "
  USAGE: $program_name [-v|--verbose] [-n|--dry[-]run] [--pretty] [--nointeract]
         $indent_space [-e|--edit] [--run=<command>] [--confirm] [--auto-undo]
         $indent_space [--fail-if-conflict] [-m <msg>|--message=<msg>]
         $indent_space [--author=<name>] [<git-merge-options>] <branch-to-merge>\n
         --verbose    : print each underlying git command before execution
         --dryrun     : just print git commands, but do not execute them
         --pretty     : use ANSI colors to highlight step messages
         --nointeract : avoid asking confirmations (assume everything OK)
         --edit       : force editing of files with conflicts after merge
                        (normally this only happens if 'mergetool' fails)
         --run=<cmd>  : run 'cmd' before committing the merge (abort if fails)
         --confirm    : ask confirmation before committing the merge
                        (normally this is only done in case of conflicts)
         --auto-undo  : do the merge and then undo (test conflicts resolution)
         --fail-if-conflict : undo merge if manual conflict resolution needed
                              (use along with --nointeract for batch invocation)
         --message    : use <msg> as comment for merge commit (if not ff)
         --author     : use <name> as author of the merge commit (if not ff)\n
         All other options are passed along to 'git merge'.\n\n";
    exit 0;
  }

  $COLORS{I} = $COLORS{B} = $COLORS{N} = $COLORS{S} = '' unless ( $OPTIONS{pretty} );

  my $branch_found = FALSE;
  foreach my $arg (@ARGV) {
    if ( $branch_found || $arg =~ /^-/ ) {
      push( @{$options_for_merge}, $arg );
    } else {
      ${$branch_to_merge} = $arg;
      $branch_found = TRUE;
    }
  } # END foreach

  die "No <branch-to-merge> specified!\n" unless ( $branch_found );
}

#==============================================================================

sub Info {
  print "\t$COLORS{I}$COLORS{S}@_ ...$COLORS{S}$COLORS{N}\n";
}

#==============================================================================

sub AskConfirmation {
  # ----- Function arguments -----
  my( $msg, $default ) = @_;
  # ----- Function code -----
  return $default unless ( $OPTIONS{interact} );
  while ( TRUE ) {
    printf '%s [Y/N] » ', $msg;
    if ( $OPTIONS{dryrun} ) {
      print '', ($default ? 'Y' : 'N'), " [dryrun]\n";
      return $default;
    }
    chomp( my $answer = <STDIN> );
    return TRUE  if ( $answer =~ /^y$/i );
    return FALSE if ( $answer =~ /^n$/i );
  } # END while
}

#==============================================================================
#==============================================================================

package GitDoMerge;

use IO::Handle;

use lib ( __FILE__ =~ m!^(.*)/[^\/]+$! );  # Next module can be in same dir
use base 'GitCmd';

use constant TRUE  => GitCmd::TRUE;
use constant FALSE => GitCmd::FALSE;

#------------------------------------------------------------------------------

sub CurrentBranch {
  # ----- Local variables -----
  my( $self ) = @_;
  # ----- Function code -----
  my $current = $self->symbolic_ref( 'HEAD' );
  $current =~ s!^refs/heads/!!;
  # ----- Return value -----
  $current;
}

#------------------------------------------------------------------------------

sub MergeInProgress {
  # ----- Function arguments -----
  my( $self ) = @_;
  # ----- Function code -----
  my $git_dir = $self->rev_parse( '--git-dir' );
  # ----- Return value -----
  -f "$git_dir/MERGE_HEAD";
}

#------------------------------------------------------------------------------

# module return code
TRUE;

#==============================================================================
#==============================================================================
